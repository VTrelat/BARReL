import POGReader.Basic
import Barrel.Meta
import Barrel.Builtins

open Std Lean Meta Elab Term

namespace B

  def reservedVarToExpr : (k : String) ‚Üí TermElabM Lean.Expr
    | "MININT", _ => return mkConst ``Builtins.MININT
    | "MAXINT", _ => return mkConst ``Builtins.MAXINT
    | "NAT", _ => return mkConst ``Builtins.NAT
    | "NAT1", _ => return mkConst ``Builtins.NAT‚ÇÅ
    | "NATURAL", _ => return mkConst ``Builtins.NATURAL
    | "NATURAL1", _ => return mkConst ``Builtins.NATURAL‚ÇÅ
    | "INT", _ => return mkConst ``Builtins.INT
    | "INTEGER", _ => return mkConst ``Builtins.INTEGER
    | "BOOL", _ => return mkConst ``Builtins.BOOL
    | "FLOAT", _ => return mkConst ``Builtins.FLOAT
    | "REAL", _ => return mkConst ``Builtins.REAL
    | v, _ => throwError "Variable {v} is not reserved."

  def Syntax.Typ.toExpr : Typ ‚Üí Expr
    | .int => Int.mkType
    | .bool => .sort .zero
    | .real => mkConst ``Real
    | .pow Œ± => mkApp (.const ``Set [0]) (Œ±.toExpr)
    | .prod Œ± Œ≤ => mkApp2 (.const ``Prod [0, 0]) Œ±.toExpr Œ≤.toExpr

  private def newMVar (type? : Option Lean.Expr) : MetaM Expr := do
    -- let mvar ‚Üê pure Int.mkType
    let mvar ‚Üê Meta.mkFreshExprMVar type?
    trace[barrel] "New metavariable {mvar}"
    return mvar

  private def assignMVar (Œ≤ ty : Expr) : MetaM PUnit := do
    if !(‚Üê Œ≤.mvarId!.isAssigned) && (‚Üê Meta.isDefEq (‚Üê Œ≤.mvarId!.getType) (‚Üê inferType ty)) then
      trace[barrel] m!"Assigning metavariable {Œ≤} to {ty}"
      Œ≤.mvarId!.assign ty

  private def newLMVar : MetaM Level := do
    let lmvar ‚Üê Meta.mkFreshLevelMVar
    trace[barrel] "New level metavariable {lmvar}"
    return lmvar

  private partial def getSetElemType (ty : Expr) : MetaM Expr := do
    let rec loop (t : Expr) (didWhnf : Bool) : MetaM Expr := do
      match t with
      | .app (.const ``Set _) Œ± => pure Œ±
      | .forallE n dom body bi =>
          Meta.withLocalDecl n bi dom fun x => do
            let body' := body.instantiate1 x
            if (‚Üê Meta.isProp body') then
              return dom
            else if didWhnf then
              throwError "Expected a set type, got {t}"
            else
              loop (‚Üê Meta.whnf t) true
      | _ =>
          let t' ‚Üê Meta.whnf t
          if didWhnf || t' == t then
            throwError "Expected a set type, got {t}"
          else
            loop t' true
    loop ty false

  private partial def flattenProdType : Expr ‚Üí Nat ‚Üí MetaM (List Expr)
    | .app (.app (.const ``Prod _) Œ±) Œ≤, n + 1 => do
        return (‚ÜêflattenProdType Œ± n).concat Œ≤
    | ty, _ + 1 => throwError "Expected a product type, got {ty}"
    | ty, 0 => return [ty]

  private partial def mkProdTuple : List Expr ‚Üí MetaM Expr
    | [] => throwError "mkProdTuple: empty tuple"
    | [x] => pure x
    | x :: xs => do
        let tail ‚Üê mkProdTuple xs
        mkAppM ``Prod.mk #[x, tail]

  private def lookupVar (x : String) : TermElabM Expr := do
    let some e := (‚Üê getLCtx).findFromUserName? (.mkStr1 x)
      | throwError "No variable {x} found in context"
    return e.toExpr

  partial def _root_.Lean.Expr.getForallHeads : Lean.Expr ‚Üí List Lean.Expr
    | .forallE _ t b _ => t :: b.getForallHeads
    | .mdata _ b => b.getForallHeads
    | _ => []

  private def _root_.Lean.Meta.mkUnaryOp (className : Name) (opName : Name) (a : Expr) : MetaM Expr := do
    let aType ‚Üê inferType a
    let u ‚Üê getDecLevel aType
    let inst ‚Üê synthInstance (mkApp (mkConst className [u]) aType)
    return mkApp3 (mkConst opName [u]) aType inst a

  private def _root_.Lean.Meta.mkNeg (a : Expr) : MetaM Expr := Lean.Meta.mkUnaryOp ``Neg ``Neg.neg a

  mutual
    partial def makeBinary (f : Name) (t‚ÇÅ t‚ÇÇ : Syntax.Term) : TermElabM Expr := do
      mkAppM f #[‚Üê t‚ÇÅ.toExpr, ‚Üê t‚ÇÇ.toExpr]

    partial def makeUnary (f : Name) (t : Syntax.Term) : TermElabM Expr := do
      mkAppM f #[‚Üê t.toExpr]

    partial def Syntax.Term.toExpr : Syntax.Term ‚Üí TermElabM Expr
      | .var v => if v ‚àà B.Syntax.reservedIdentifiers then reservedVarToExpr v else lookupVar v
      | .int n => return mkIntLit n
      | .uminus x => do mkNeg (‚Üêx.toExpr)
      | .le x y => do mkLE (‚Üê x.toExpr) (‚Üê y.toExpr)
      | .lt x y => do mkLT (‚Üê x.toExpr) (‚Üê y.toExpr)
      | .bool b => return mkConst (if b then ``True else ``False)
      | .maplet x y => makeBinary ``Prod.mk x y
      | .add x y => do mkAdd (‚Üêx.toExpr) (‚Üêy.toExpr)
      | .sub x y => do mkSub (‚Üêx.toExpr) (‚Üêy.toExpr)
      | .mul x y => do mkMul (‚Üêx.toExpr) (‚Üêy.toExpr)
      | .div x y => mkIntDiv <$> x.toExpr <*> y.toExpr
      | .mod x y => mkIntMod <$> x.toExpr <*> y.toExpr
      | .exp x y => do mkIntPowNat <$> x.toExpr <*> mkAppM ``Int.toNat #[‚Üê y.toExpr]
      | .and x y => do
        let w ‚Üê mkFreshUserName `h
        let x ‚Üê x.toExpr
        let lam ‚Üê withLocalDeclD w x Œª x ‚Ü¶
          liftMetaM ‚àò mkLambdaFVars #[x] =<< y.toExpr
        mkAppM ``DepAnd #[lam]
      | .or x y => mkOr <$> x.toExpr <*> y.toExpr
      | .imp x y => do
        withLocalDecl (‚Üê mkFreshBinderName) .default (‚Üê x.toExpr) Œª z ‚Ü¶
          liftMetaM ‚àò mkForallFVars #[z] =<< y.toExpr
      | .iff x y =>
        mkIff <$> x.toExpr
              <*> y.toExpr
      | .not x => mkNot <$> x.toExpr
      | .eq x y => do
        let x ‚Üê x.toExpr
        let y ‚Üê y.toExpr
        liftMetaM <| mkEq x y
      | .mem x S => makeBinary ``Membership.mem S x
      | .ùîπ => mkAppOptM ``Set.univ #[mkSort 0]
      | .‚Ñ§ => mkAppOptM ``Set.univ #[Int.mkType]
      | .‚Ñù => mkAppOptM ``Set.univ #[mkConst ``Real]
      | .collect xs P => do
        let lam ‚Üê if xs.size = 1 then
          let ‚ü®x, t‚ü© := xs[0]!

          withLocalDeclD (Name.mkStr1 x) t.toExpr Œª xvec ‚Ü¶
            liftMetaM ‚àò mkLambdaFVars #[xvec] =<< P.toExpr
        else
          let x ‚Üê mkFreshBinderName

          -- Œ± = (Œ±‚ÇÅ √ó ‚Ä¶) √ó Œ±‚Çô
          let Œ± ‚Üê xs[1:].foldlM (init := xs[0]!.snd.toExpr) fun acc ‚ü®_, œÑ·µ¢‚ü© ‚Ü¶ do
            mkAppM ``Prod #[acc, œÑ·µ¢.toExpr]

          withLocalDeclD x Œ± fun xvec ‚Ü¶ do
            let rec go_collect : List (String √ó Syntax.Typ) ‚Üí TermElabM Expr
              | [] => do
                let xs' ‚Üê do
                  xs[1:].foldlM (init := ‚Üê lookupVar xs[0]!.fst) fun acc ‚ü®x·µ¢, _‚ü© ‚Ü¶ do
                    mkAppM ``Prod.mk #[acc, ‚Üê lookupVar x·µ¢]
                -- xÃÑ = xs'
                let eq : Expr ‚Üê mkEq xvec xs'
                -- xÃÑ = xs' ‚àß P[xÃÑ/vs]
                let lam ‚Üê withLocalDeclD (‚Üê mkFreshBinderName) eq Œª eq ‚Ü¶
                  liftMetaM ‚àò mkLambdaFVars #[eq] =<< P.toExpr
                mkAppM ``DepAnd #[lam]
              | ‚ü®x, t‚ü© :: xs => do
                let lam ‚Üê withLocalDeclD (Name.mkStr1 x) (t.toExpr) fun y ‚Ü¶ do
                  (liftMetaM ‚àò mkLambdaFVars #[y] =<< go_collect xs)
                mkAppM ``Exists #[lam]

            liftMetaM ‚àò mkLambdaFVars #[xvec] =<< go_collect xs.toList

        mkAppM ``setOf #[lam]
      | .all xs P => do
        let rec go_forall : List (String √ó Syntax.Typ) ‚Üí TermElabM Expr
          | [] => P.toExpr
          | ‚ü®x, t‚ü© :: xs => do
            withLocalDeclD (Name.mkStr1 x) (t.toExpr) fun y ‚Ü¶ do
              (liftMetaM ‚àò mkForallFVars #[y] =<< go_forall xs)

        go_forall xs.toList
      | .exists xs P => do
        let rec go_exists : List (String √ó Syntax.Typ) ‚Üí TermElabM Expr
          | [] => P.toExpr
          | ‚ü®x, t‚ü© :: xs => do
            let lam ‚Üê withLocalDeclD (Name.mkStr1 x) (t.toExpr) fun y ‚Ü¶ do
              (liftMetaM ‚àò mkLambdaFVars #[y] =<< go_exists xs)
            mkAppM ``Exists #[lam]

        go_exists xs.toList
      | .lambda xs P F => do
        -- { z | ‚àÉ x‚ÇÅ ‚Ä¶ x‚Çô, ‚àÉ y, z = ((x‚ÇÅ, ‚Ä¶, x‚Çô), y) ‚àß D ‚àß y = F }

        -- Œ± = (Œ±‚ÇÅ √ó ‚Ä¶) √ó Œ±‚Çô
        let Œ± ‚Üê xs[1:].foldlM (init := xs[0]!.snd.toExpr) fun acc ‚ü®_, œÑ·µ¢‚ü© ‚Ü¶ do
          mkAppM ``Prod #[acc, œÑ·µ¢.toExpr]
        let levelŒ± ‚Üê getDecLevel Œ±

        -- Œ≤ is the return type of the function
        let lmvar ‚Üê newLMVar
        let Œ≤ ‚Üê newMVar (mkSort <| .succ lmvar)

        let Œ≥ := mkApp2 (mkConst ``Prod [levelŒ±, lmvar]) Œ± Œ≤

        let z ‚Üê mkFreshBinderName
        let lam ‚Üê withLocalDeclD z Œ≥ fun zvec ‚Ü¶ do
          let rec go : List (String √ó Syntax.Typ) ‚Üí TermElabM Expr
            | [] => do
              let y ‚Üê mkFreshBinderName
              let lam ‚Üê withLocalDeclD y Œ≤ fun y ‚Ü¶ do
                -- compute return type of F: this consumes F already
                let _ ‚Üê assignMVar Œ≤ (‚Üê inferType (‚Üê F.toExpr))
                let Œ≤ ‚Üê instantiateMVars Œ≤

                let xs' ‚Üê do
                  xs[1:].foldlM (init := ‚Üê lookupVar xs[0]!.fst) fun acc ‚ü®x·µ¢, _‚ü© ‚Ü¶ do
                    mkAppM ``Prod.mk #[acc, ‚Üê lookupVar x·µ¢]
                -- xÃÑ = (xs', y)
                let eq : Expr ‚Üê mkEq zvec (mkApp4 (mkConst ``Prod.mk [levelŒ±, lmvar]) Œ± Œ≤ xs' y)
                -- y = F[xÃÑ/xs']
                -- let eqF : Expr ‚Üê mkEq y F
                -- xÃÑ = (xs', y) ‚àß P[xÃÑ/xs'] ‚àß y = F[xÃÑ/xs']

                let n‚ÇÅ ‚Üê mkFreshUserName `h‚ÇÅ
                let n‚ÇÇ ‚Üê mkFreshUserName `h‚ÇÇ
                let lam ‚Üê withLocalDeclD n‚ÇÅ eq Œª eq ‚Ü¶
                  liftMetaM ‚àò mkLambdaFVars #[eq] =<< do
                    withLocalDeclD n‚ÇÇ (‚Üê P.toExpr) Œª P ‚Ü¶ do
                      let eq' ‚Üê mkEq y (‚ÜêF.toExpr) -- no other choice
                      mkAppM ``DepAnd #[‚ÜêliftMetaM <| mkLambdaFVars #[P] eq']

                mkLambdaFVars #[y] (‚Üê mkAppM ``DepAnd #[lam])
              mkAppM ``Exists #[lam]
            | ‚ü®x, t‚ü© :: xs => do
              let lam ‚Üê withLocalDeclD (Name.mkStr1 x) (t.toExpr) fun y =>
                (liftMetaM ‚àò mkLambdaFVars #[y] =<< go xs)
              mkAppM ``Exists #[lam]

          liftMetaM ‚àò mkLambdaFVars #[zvec] =<< go xs.toList
        mkAppM ``setOf #[lam]
      | .interval lo hi => makeBinary ``Builtins.interval lo hi
      | .subset S T => makeBinary ``HasSubset.Subset S T
      | .set es ty => do
        if es.isEmpty then
          mkAppOptM ``EmptyCollection.emptyCollection #[ty.toExpr, .none]
        else
          let emp ‚Üê mkAppOptM ``Singleton.singleton #[.none, ty.toExpr, .none, ‚Üê es.back!.toExpr]
          es.pop.foldrM (init := emp) fun e acc ‚Ü¶ do mkAppM ``Insert.insert #[‚Üêe.toExpr, acc]
      | .setminus S T => makeBinary ``SDiff.sdiff S T
      | .pow S => makeUnary ``Set.powerset S
      | .pow‚ÇÅ S => makeUnary ``Builtins.POW‚ÇÅ S
      | .cprod S T => makeBinary ``SProd.sprod S T
      | .union S T => makeBinary ``Union.union S T
      | .inter S T => makeBinary ``Inter.inter S T
      | .rel A B => makeBinary ``B.Builtins.rels A B
      | .image R X => makeBinary ``SetRel.image R X
      | .inv R => makeUnary ``SetRel.inv R
      | .id A => makeUnary ``B.Builtins.id A
      | .dom f => makeUnary ``B.Builtins.dom f
      | .ran f => makeUnary ``B.Builtins.ran f
      | .domRestr R E => makeBinary ``B.Builtins.domRestr E R
      | .domSubtr R E => makeBinary ``B.Builtins.domSubtr E R
      | .codomRestr R E => makeBinary ``B.Builtins.codomRestr R E
      | .codomSubtr R E => makeBinary ``B.Builtins.codomSubtr R E
      | .fun A B isPartial =>
        makeBinary (if isPartial then ``B.Builtins.pfun else ``B.Builtins.tfun) A B
      | .injfun A B isPartial => do
        makeBinary (if isPartial then ``B.Builtins.injPFun else ``B.Builtins.injTFun) A B
      | .surjfun A B isPartial => do
        makeBinary (if isPartial then ``B.Builtins.surjPFun else ``B.Builtins.surjTFun) A B
      | .bijfun A B isPartial => do
        makeBinary (if isPartial then ``B.Builtins.bijPFun else ``B.Builtins.bijTFun) A B
      | .min S => do
        let S ‚Üê S.toExpr
        let wfMVar ‚Üê liftMetaM ‚àò newMVar =<< mkAppM ``B.Builtins.min.WF #[S]
        mkAppM ``B.Builtins.min #[S, wfMVar]
      | .max S => do
        let S ‚Üê S.toExpr
        let wfMVar ‚Üê liftMetaM ‚àò newMVar =<< mkAppM ``B.Builtins.max.WF #[S]
        mkAppM ``B.Builtins.max #[S, wfMVar]
      | .app f x => do
        let f ‚Üê f.toExpr
        let x ‚Üê x.toExpr
        let wfMVar ‚Üê liftMetaM ‚àò newMVar =<< mkAppM ``B.Builtins.app.WF #[f, x]
        mkAppM ``B.Builtins.app #[f, x, wfMVar]
      | .fin S => makeUnary ``B.Builtins.FIN S
      | .fin‚ÇÅ S => makeUnary ``B.Builtins.FIN‚ÇÅ S
      | .card S => do
        let S ‚Üê S.toExpr
        let wfMVar ‚Üê liftMetaM ‚àò newMVar =<< mkAppM ``B.Builtins.card.WF #[S]
        mkAppM ``B.Builtins.card #[S, wfMVar]

  end

  def POG.Goal.toExpr (sg : POG.Goal) : TermElabM (Expr √ó Array (Expr √ó MVarId)) := do
    -- trace[barrel.pog] s!"Encoding: {goal}"

    let vars : Array (Name √ó (Array Expr ‚Üí TermElabM Expr)) :=
      sg.vars.map Œª ‚ü®x, œÑ‚ü© ‚Ü¶ ‚ü®.mkStr1 x, Œª _ ‚Ü¶ pure œÑ.toExpr‚ü©

    let g ‚Üê Meta.withLocalDeclsD vars Œª vars ‚Ü¶ do
      -- let rec goHyp : List Syntax.Term ‚Üí TermElabM Expr
      --   | [] => checkpoint sg.goal.toExpr pure
      --   | t :: ts => checkpoint t.toExpr Œª t ‚Ü¶ mkForall `_ .default t <$> goHyp ts

      trace[barrel] "Decoded goal: {sg.goal}"

      let g ‚Üê sg.goal.toExpr

      trace[barrel] "Generated goal (no quantified variable): {indentExpr g}"

      let g ‚Üê liftMetaM (mkForallFVars vars (usedOnly := true) g)
              >>= Term.ensureHasType (.some <| .sort 0)
      Meta.check g
      instantiateMVars g

    let mvars := g.collectMVars {} |>.result

    trace[barrel] "Generated goal: {indentExpr g}"

    let mut wfs := #[]
    let mut i := 0

    for mvar in mvars do
      let ty ‚Üê mvar.withContext do
        liftMetaM ‚àò mkForallFVars (‚Üê getLCtx).getFVars =<< mvar.getType

      trace[barrel.wf] "WF metavariable to solve {sg.name}.wf_{(i : Nat)} (?{mvar.name}):{indentExpr ty}"

      wfs := wfs.push (ty, mvar)

    return (g, wfs)

end B
